# Note: Tij stands for Stress Energy tensor.
# IF stands for ideal fluid, CTS for conformal thin sandwich method;
# thus, Tij_IF_CTS means stress enerfy of ideal fluid in CTS method. 
# given all of the fields needed for 
# S = T(mu,nu)*gamma(i,j)*gamma(-i,-mu)*gamma(-j,-nu)
# it builds "total momentum density * psi^6", 
# where psi is conformal factor, and puts it to _S. 
# note: if patch does not contain fluid, it does nothing.
# note: it depends on u0, so better first to call Tij_IF_u0 function.

# Manifold or grid Dimension
Dimension = 3;

# point on manifold shown by:
Point = ijk;

C_macro = READ_v(name);
C_macro2 = REALLOC_v_WRITE_v(name);
Ccode["#include "Tij_header.h"\n\n"];
Ccode["void Tij_IF_CTS_nonflat_psi6S(Patch_T *const patch)"];
Ccode["{"];

`  if (!IsItNSPatch(patch))`
`  {`
`    if (_Ind("_S") >= 0)`
`    {`
`      REALLOC_v_WRITE_v(_S)`
`      UNUSED(_S)`
`    }`
`    return;`
`  }`

Ccode["  const unsigned nn = patch->nn;"];
Ccode["  unsigned ijk;"];

Declare = 
{

 # conformal metric 
 (obj = Field,name = _gamma, rank = DD, C_macro);

 # conformal metric inverse
 (obj = Field,name = _gammaI, rank = UU, C_macro);

 # enthalpy
 (obj = Field,name = enthalpy, rank = 0, C_macro);

 # spin part of fluid
 (obj = Field,name = W, rank = U, C_macro);

 # d(phi)/d? for irrotional part of fluid
 (obj = Field,name = dphi, rank = D, C_macro);

 # conformal factor
 (obj = Field,name = psi, rank = 0, C_macro);

 # rho0
 (obj = Field,name = rho0, rank = 0, C_macro);

 # pressure
 (obj = Variable,name = p, rank = 0, None);

 # _S
 (obj = Field,name = _S, rank = 0, C_macro2);


}

# symmetries:
Symm[_gammaI(i,j)  = _gammaI(j,i)];
Symm[_gamma(i,j)   = _gamma(j,i)];

# calculate _S:
Ccode["  EoS_T *eos = initialize_EoS();"];
Ccode["  for(ijk = 0; ijk < nn; ++ijk)"];
Ccode["  {"];
Ccode["    eos->h   = enthalpy[ijk];"];
Ccode["    double p = eos->pressure(eos);"];
     psim4 = psi**(-4);
     psi4  = psi**(4);
     psi6  = psi**(6);
     P2    = psim4*_gammaI(i,j)*dphi(-i)*dphi(-j)+2*dphi(-i)*W(i)+psi4*_gamma(-i,-j)*W(i)*W(j);
     Sbar = psi6*(rho0*P2/enthalpy+3*p);
     Ccode["  _S[ijk] = Sbar;"];
Ccode["  }"];
Ccode["  free_EoS(eos);"];

Ccode["}"];
